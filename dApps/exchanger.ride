{-# STDLIB_VERSION 4 #-}
{-# CONTENT_TYPE DAPP #-}
{-# SCRIPT_TYPE ACCOUNT #-}

let version = "1.0.0"

let keyVersion = "version"
let keyActive = "active"
let keyAssetIdA = "A_asset_id"
let keyAssetIdB = "B_asset_id"
let keyBalanceA = "A_asset_balance"
let keyBalanceB = "B_asset_balance"
let keyShareAssetId = "share_asset_id"
let keyShareAssetSupply = "share_asset_supply"
let keyCommission = "commission"
let keyCommissionScaleDelimiter = "commission_scale_delimiter"
let keyCause = "shutdown_cause"

let adminPubKey1 = base58'${adminPubKey1}'
let adminPubKey2 = base58'${adminPubKey2}'
let adminPubKey3 = base58'${adminPubKey3}'
let adminPubKey4startStop = base58'${adminPubKey4startStop}'

let governanceAddress = Address(base58'${governanceAddress}')
let stakingAddress = Address(base58'${stakingAddress}')
let USDNid = base58'DG2xFkPdDwKUoBkzGAhQtLpSGzfXLiCYPEzeKH2Ad24p'
let stakingFeeInUSDN = 9*assetInfo(USDNid).value().minSponsoredFee.value() # sponsored fee for invoke called from scripted account

let isActive = this.getBooleanValue(keyActive)
let strAssetIdA = this.getStringValue(keyAssetIdA)
let strAssetIdB = this.getStringValue(keyAssetIdB)
let assetIdA = if strAssetIdA == "WAVES" then unit else strAssetIdA.fromBase58String()
let assetIdB = if strAssetIdB == "WAVES" then unit else strAssetIdB.fromBase58String()
let assetNameA = match assetIdA {
    case id: ByteVector => assetInfo(id).value().name
    case waves: Unit => "WAVES"
}
let assetNameB = match assetIdB {
    case id: ByteVector => assetInfo(id).value().name
    case waves: Unit => "WAVES"
}
let balanceA = this.getIntegerValue(keyBalanceA)
let balanceB = this.getIntegerValue(keyBalanceB)
let shareAssetId = this.getStringValue(keyShareAssetId).fromBase58String()
let shareAssetSupply = this.getIntegerValue(keyShareAssetSupply)


let commission = 3000 # commission/commissionScaleDelimiter = 0.003
let commissionGovernance = 1200 # commissionGovernance/commissionScaleDelimiter = 0.0012
let commissionScaleDelimiter = 1000000

let scaleValue3 = 1000
let scaleValue8 = 100000000
let slippageToleranceDelimiter = 1000
let scaleValue8Digits = 8


func accountBalance(assetId: ByteVector|Unit) = match assetId {
    case id: ByteVector => this.assetBalance(id)
    case waves: Unit => this.wavesBalance().available
}

let balanceInStakingUSDN = match getInteger(stakingAddress, "rpd_balance_" + toBase58String(USDNid) + "_" + toString(this)) {
        case amt: Unit => 0
        case amt:Int => amt
        case _  => throw("Error while extracting staking information")
  }

let balanceAccountPlusStakingA = if assetIdA == USDNid then  accountBalance(assetIdA) + balanceInStakingUSDN else accountBalance(assetIdA)
let balanceAccountPlusStakingB = if assetIdB == USDNid then  accountBalance(assetIdB) + balanceInStakingUSDN else accountBalance(assetIdB)

let hasEnoughBalance = balanceAccountPlusStakingA >= balanceA && balanceAccountPlusStakingB >= balanceB

func getAssetInfo(assetId: ByteVector|Unit) = match assetId {
    case id: ByteVector =>
        let stringId = id.toBase58String()
        let info = assetInfo(id).valueOrErrorMessage("Asset " + stringId + " doesn't exist")
        (stringId, info.name, info.decimals)
    case waves: Unit => ("WAVES", "WAVES", 8)
}

func suspend(cause: String) = [
    BooleanEntry(keyActive, false),
    StringEntry(keyCause, cause)
]

func deductCommissionForStakingOperation(amount:Int, asssetId: ByteVector|Unit) = {
    if asssetId == USDNid then amount - stakingFeeInUSDN else amount
}

@Callable(i)
func init() = {
    let (pmtAmountA, pmtAssetIdA) = (i.payments[0].amount, i.payments[0].assetId)
    let (pmtAmountB, pmtAssetIdB) = (i.payments[1].amount, i.payments[1].assetId)
    let (pmtStrAssetIdA, pmtAssetNameA, pmtDecimalsA) = getAssetInfo(pmtAssetIdA)
    let (pmtStrAssetIdB, pmtAssetNameB, pmtDecimalsB) = getAssetInfo(pmtAssetIdB)
    if this.getBoolean(keyActive).isDefined() then
        throw("DApp is already active")
    else if pmtAssetIdA == pmtAssetIdB then
        throw("Assets must be different")
    else {
        let shareName = "s" + pmtAssetNameA.take(7) + "_" + pmtAssetNameB.take(7)
        let shareDescription = "ShareToken of SwopFi protocol for " + pmtAssetNameA + " and " + pmtAssetNameB + " at address " + this.toString()

        let shareDecimals = (pmtDecimalsA + pmtDecimalsB) / 2
        let shareInitialSupply = fraction(
            pow(pmtAmountA, pmtDecimalsA, 5, 1, pmtDecimalsA, HALFDOWN),
            pow(pmtAmountB, pmtDecimalsB, 5, 1, pmtDecimalsB, HALFDOWN),
            pow(10, 0, shareDecimals, 0, 0, HALFDOWN)
        )
        let shareIssue = Issue(shareName, shareDescription, shareInitialSupply, shareDecimals, true)
        let shareIssueId = shareIssue.calculateAssetId()
        [
            StringEntry(keyVersion, version),
            BooleanEntry(keyActive, true),
            StringEntry(keyAssetIdA, pmtStrAssetIdA),
            StringEntry(keyAssetIdB, pmtStrAssetIdB),
            IntegerEntry(keyBalanceA, pmtAmountA),
            IntegerEntry(keyBalanceB, pmtAmountB),
            IntegerEntry(keyCommission, commission),
            IntegerEntry(keyCommissionScaleDelimiter, commissionScaleDelimiter),
            shareIssue,
            StringEntry(keyShareAssetId, shareIssueId.toBase58String()),
            IntegerEntry(keyShareAssetSupply, shareInitialSupply),
            ScriptTransfer(i.caller, shareInitialSupply, shareIssueId)
        ]
    }
}

@Callable(i)
func replenishWithTwoTokens(slippageTolerance: Int) = {
    let (pmtAmountAwithoutTxStakingFee, pmtAssetIdA) = (i.payments[0].amount, i.payments[0].assetId)
    let (pmtAmountBwithoutTxStakingFee, pmtAssetIdB) = (i.payments[1].amount, i.payments[1].assetId)
    
    # block for accounting the cost of commissions for staking operations
    let pmtAmountA = deductCommissionForStakingOperation(pmtAmountAwithoutTxStakingFee, pmtAssetIdA)
    let pmtAmountB = deductCommissionForStakingOperation(pmtAmountBwithoutTxStakingFee, pmtAssetIdB) 

    # fraction should be equal 1(multiple by 1000) if depositor replenish with proportion according to actual price
    let tokenRatio = fraction(balanceA, scaleValue8, pmtAmountA).fraction(scaleValue3,fraction(balanceB, scaleValue8, pmtAmountB))
    let ratioShareTokensInA = fraction(pmtAmountA, scaleValue8, balanceA)
    let ratioShareTokensInB = fraction(pmtAmountB, scaleValue8, balanceB)
    let shareTokenToPayAmount = min([ratioShareTokensInA, ratioShareTokensInB]).fraction(shareAssetSupply, scaleValue8)

    if !isActive then
        throw("DApp is inactive at this moment")
    else if slippageTolerance < 0 || slippageTolerance > slippageToleranceDelimiter then
        throw("Slippage tolerance must be between 0 and " + slippageToleranceDelimiter.toString() + " inclusively. Actual: " + slippageTolerance.toString())
    else if i.payments.size() != 2 then
        throw("Two attached assets expected")
    else if pmtAssetIdA != assetIdA || pmtAssetIdB != assetIdB then
        throw("Incorrect assets attached. Expected: " + strAssetIdA + " and " + strAssetIdB)
    else if tokenRatio < (scaleValue3 * (slippageToleranceDelimiter - slippageTolerance)) / slippageToleranceDelimiter
            || tokenRatio > (scaleValue3 * (slippageToleranceDelimiter + slippageTolerance)) / slippageToleranceDelimiter then
        throw("Incorrect assets amount: amounts must have the contract ratio")
    else if shareTokenToPayAmount == 0 then
        throw("Too small amount to replenish")
    else if !hasEnoughBalance then
        suspend("Suspicious state. Actual balances: " + balanceA.toString() + " " + assetNameA + ", " + balanceB.toString() + " " + assetNameB
        + ". State: " + accountBalance(assetIdA).toString() + " " + assetNameA + ", " + accountBalance(assetIdB).toString() + " " + assetNameB)
    else [
        IntegerEntry(keyBalanceA, balanceA + pmtAmountA),
        IntegerEntry(keyBalanceB, balanceB + pmtAmountB),
        IntegerEntry(keyShareAssetSupply, shareAssetSupply + shareTokenToPayAmount),
        Reissue(shareAssetId, shareTokenToPayAmount, true),
        ScriptTransfer(i.caller, shareTokenToPayAmount, shareAssetId)
    ]
}

@Callable(i)
func withdraw() = {
    let (pmtAmount, pmtAssetId) = (i.payments[0].amount, i.payments[0].assetId)
    let amountToPayWithoutFeeA = fraction(pmtAmount, balanceA, shareAssetSupply)
    let amountToPayWithoutFeeB = fraction(pmtAmount, balanceB, shareAssetSupply)
    # block for accounting the cost of commissions for staking operations
    let amountToPayA = deductCommissionForStakingOperation(amountToPayWithoutFeeA, assetIdA)
    let amountToPayB = deductCommissionForStakingOperation(amountToPayWithoutFeeB, assetIdB)
    if !isActive then
        throw("DApp is inactive at this moment")
    else if i.payments.size() != 1 then
        throw("One attached payment expected")
    else if pmtAssetId != shareAssetId then
        throw("Incorrect asset attached. Expected: " + shareAssetId.toBase58String())
    else if !hasEnoughBalance then
        suspend("Suspicious state. Actual balances: " + balanceA.toString() + " " + assetNameA + ", " + balanceB.toString() + " " + assetNameB
        + ". State: " + accountBalance(assetIdA).toString() + " " + assetNameA + ", " + accountBalance(assetIdB).toString() + " " + assetNameB)
    else [
        IntegerEntry(keyBalanceA, balanceA - amountToPayA),
        IntegerEntry(keyBalanceB, balanceB - amountToPayB),
        IntegerEntry(keyShareAssetSupply, shareAssetSupply - pmtAmount),
        Burn(shareAssetId, pmtAmount),
        ScriptTransfer(i.caller, amountToPayA, assetIdA),
        ScriptTransfer(i.caller, amountToPayB, assetIdB)
    ]
}

@Callable(i)
func exchange(minAmountToReceive: Int) = {
    let (pmtAmount, pmtAssetId) = (i.payments[0].amount, i.payments[0].assetId)

    func calculateFees(tokenFrom: Int, tokenTo: Int) = {
        let amountWithoutFee = fraction(tokenTo, pmtAmount, pmtAmount + tokenFrom)
        let amountWithFee = fraction(amountWithoutFee, commissionScaleDelimiter - commission, commissionScaleDelimiter)
        let governanceReward = fraction(amountWithoutFee, commissionGovernance, commissionScaleDelimiter)

        if amountWithFee < minAmountToReceive then
            throw("Calculated amount to receive " + amountWithFee.toString() + " is less than specified minimum " + minAmountToReceive.toString())
        else
            (amountWithoutFee, amountWithFee, governanceReward)
    }

    if !isActive then
        throw("DApp is inactive at this moment")
    else if minAmountToReceive <= 0 then
        throw("Minimal amount to receive must be positive. Actual: " + minAmountToReceive.toString())
    else if i.payments.size() != 1 then
        throw("One attached payment expected")
    else if !hasEnoughBalance then
        suspend("Suspicious state. Actual balances: " + balanceA.toString() + " " + assetNameA + ", " + balanceB.toString() + " " + assetNameB
        + ". State: " + accountBalance(assetIdA).toString() + " " + assetNameA + ", " + accountBalance(assetIdB).toString() + " " + assetNameB)
    else if pmtAssetId == assetIdA then {
        let assetIdSend = assetIdB
        let (amountWithoutFee, amountWithFee, governanceReward) = calculateFees(balanceA, balanceB)

        # successful execution result is updating information about actual balance and supply into the state and transfer tokens to the caller
        [
            IntegerEntry(keyBalanceA, balanceA + pmtAmount),
            IntegerEntry(keyBalanceB, balanceB - amountWithFee - governanceReward),
            ScriptTransfer(i.caller, amountWithFee, assetIdSend),
            ScriptTransfer(governanceAddress, governanceReward, assetIdSend)
        ]
    } else if pmtAssetId == assetIdB then {
        let assetIdSend = assetIdA
        let (amountWithoutFee, amountWithFee, governanceReward) = calculateFees(balanceB, balanceA)

        # successful execution result is updating information about actual balance and supply into the state and transfer tokens to the caller
        [
            IntegerEntry(keyBalanceA, balanceA - amountWithFee - governanceReward),
            IntegerEntry(keyBalanceB, balanceB + pmtAmount),
            ScriptTransfer(i.caller, amountWithFee, assetIdSend),
            ScriptTransfer(governanceAddress, governanceReward, assetIdSend)
        ]
    } else
        throw("Incorrect asset attached. Expected: " + strAssetIdA + " or " + strAssetIdB)
}

@Callable(i)
func shutdown() = if [adminPubKey1, adminPubKey2, adminPubKey3,adminPubKey4startStop].containsElement(i.callerPublicKey) then
    suspend("Paused by admin")
else
    throw("Only admin can call this function")

@Callable(i)
func activate() = if [adminPubKey1, adminPubKey2, adminPubKey3,adminPubKey4startStop].containsElement(i.callerPublicKey) then [
    BooleanEntry(keyActive, true),
    DeleteEntry(keyCause)
] else
    throw("Only admin can call this function")
    
@Callable(i)
func takeIntoAccountExtraFunds(amountLeave: Int) = {
    if [adminPubKey1, adminPubKey2, adminPubKey3,adminPubKey4startStop,adminPubKey5staking].containsElement(i.callerPublicKey) then  
            let uncountableAmountEnrollAssetA = (balanceAccountPlusStakingA - balanceA)
            let uncountableAmountEnrollAssetB = (balanceAccountPlusStakingB - balanceB)
            if uncountableAmountEnrollAssetA < 0 || uncountableAmountEnrollAssetB < 0 then suspend("Enroll amount negative") else
            let amountEnrollA = if assetIdA == USDNid then uncountableAmountEnrollAssetA - amountLeave else uncountableAmountEnrollAssetA
            let amountEnrollB = if assetIdB == USDNid then uncountableAmountEnrollAssetB - amountLeave else uncountableAmountEnrollAssetB
            if amountEnrollA < 0 || amountEnrollB < 0 then throw("Too large amountLeave") else
            [
                IntegerEntry(keyBalanceA, balanceA + amountEnrollA),
                IntegerEntry(keyBalanceB, balanceB + amountEnrollB)
            ]
    else 
        throw("Only admin can call this function")
}

@Verifier(tx)
func verify() = {
    match tx{
        case tx:InvokeScriptTransaction => {
            let adminPubKey5stakingSigned = if sigVerify(tx.bodyBytes, tx.proofs[0], adminPubKey5staking) then true else false
            let isDAppCorrect = if tx.dApp == stakingAddress then true else false
            adminPubKey5stakingSigned && isDAppCorrect
        }
        case _ =>{
            let adminPubKey1Signed = if sigVerify(tx.bodyBytes, tx.proofs[0], adminPubKey1) then 1 else 0
            let adminPubKey2Signed = if sigVerify(tx.bodyBytes, tx.proofs[1], adminPubKey2) then 1 else 0
            let adminPubKey3Signed = if sigVerify(tx.bodyBytes, tx.proofs[2], adminPubKey3) then 1 else 0
            adminPubKey1Signed + adminPubKey2Signed + adminPubKey3Signed >= 2
        }
    }
}